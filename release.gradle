
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // http://github-api.kohsuke.org/
        classpath "org.kohsuke:github-api:1.79"
    }
}

def ext = project.rootProject.ext

assert ext.version
assert ext.githubOwner
assert ext.githubRepo
assert ext.artifactFile.exists()
assert ext.artifactContentType

task('release', dependsOn: stage) {
    doLast {
        println("release for ${ext.version}")

        final github = org.kohsuke.github.GitHub.connect()
        final repo = github.getRepository("${ext.githubOwner}/${ext.githubRepo}")

        final release = repo.createRelease("v${ext.version}")
            .name("v${ext.version}")
            .body("Version v${version} of ${ext.githubOwner}/${ext.githubRepo}.")
            .commitish("master")
            .create();

        release.uploadAsset(ext.artifactFile, ext.artifactContentType)

        println release.getHtmlUrl()
    }
}
